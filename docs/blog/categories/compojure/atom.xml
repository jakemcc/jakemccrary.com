<?xml version="1.0" encoding="utf-8"?>
<feed xmlns="http://www.w3.org/2005/Atom">

  <title><![CDATA[Category: compojure | Jake McCrary]]></title>
  <link href="https://jakemccrary.com/blog/categories/compojure/atom.xml" rel="self"/>
  <link href="https://jakemccrary.com/"/>
  <updated>2022-01-03T11:36:31-06:00</updated>
  <id>https://jakemccrary.com/</id>
  <author>
    <name><![CDATA[Jake McCrary]]></name>
    
  </author>
  <generator uri="http://octopress.org/">Octopress</generator>

  
  <entry>
    <title type="html"><![CDATA[Restricting access to certain routes]]></title>
    <link href="https://jakemccrary.com/blog/2014/12/21/restricting-access-to-certain-routes/"/>
    <updated>2014-12-21T16:48:00-06:00</updated>
    <id>https://jakemccrary.com/blog/2014/12/21/restricting-access-to-certain-routes</id>
    <content type="html"><![CDATA[<p>Recently I&rsquo;ve been working on adding authentication and authorization
to a Clojure web service. The project uses
<a href="https://github.com/weavejester/compojure">compojure</a> for routing and
<a href="https://github.com/cemerick/friend">friend</a> for authentication and
authorization. My pair and I wanted to restrict access to specific
routes while leaving some routes completely public. It took a few
tries until we figured out how to do this in a way that made us happy.</p>

<p>The rest of this post shows the approximate path we took to our
current solution. It focuses on using friend to restrict access to
specific routes. It does not go into details about adding
authentication to your web service.</p>

<p>Below is an example of the routes before adding authorization checks.</p>

<pre><code class="clojure">(ns example.server
  (:require [compojure.core :refer [GET defroutes] :as compojure]
            [compojure.route :as route]))

(defroutes app
  (GET "/status" _ (status))
  (GET "/cars" _ (fetch-cars))
  (GET "/attributes" _ (fetch-attributes))
  (GET "/drivers" _ (fetch-drivers))
  (route/not-found "NOT FOUND"))
</code></pre>

<p>We wanted to make <code>/cars</code>, <code>/attributes</code>, and <code>/drivers</code> require that
the request satisfies the <code>:example.server/user</code> role. Requesting
<code>/status</code> should not require authorization. The first attempt left us
with the following code.</p>

<pre><code class="clojure">(ns example.server
  (:require [compojure.core :refer [GET defroutes] :as compojure]
            [compojure.route :as route]
            [cemerick.friend :as friend]))

(defroutes app
  (GET "/status" _ (status))
  (GET "/cars" _
       (friend/authorize #{::user}
                         (fetch-cars)))
  (GET "/attributes" _
       (friend/authorize #{::user}
                         (fetch-attributes)))
  (GET "/drivers" _
       (friend/authorize #{::user}
                         (fetch-drivers)))
  (route/not-found "NOT FOUND"))
</code></pre>

<p>The above works but it suffers from repetition. You could write a
macro to minimize the repetition but we thought there must be a better
way.</p>

<p>After reading more of <a href="https://github.com/cemerick/friend">friend</a>&rsquo;s
documentation we discovered <code>friend/wrap-authorize</code>. This is
middleware that only allows requests through if the request
satisfies the required roles. Our first pass at using
<code>friend/wrap-authorize</code> looked like the following example.</p>

<pre><code class="clojure">(ns example.server
  (:require [compojure.core :refer [GET defroutes] :as compojure]
            [compojure.route :as route]
            [cemerick.friend :as friend]))

(defroutes protected-routes
  (GET "/cars" _ (fetch-cars))
  (GET "/attributes" _ (fetch-attributes))
  (GET "/drivers" _ (fetch-drivers)))

(defroutes app
  (GET "/status" _ (status))
  (friend/wrap-authorize protected-routes #{::user})
  (route/not-found "NOT FOUND"))
</code></pre>

<p>This is much nicer. The repetition is removed by extracting routes
that require authorization into a separate <code>defroutes</code> and wrapping it
with <code>friend/wrap-authorize</code>.</p>

<p>This introduces a subtle bug. A response with status code 404 is no
longer returned if a non-existent resource is requested and the
request is unauthorized. This is because the authorization check
happens <em>before</em> matching a route. friend&rsquo;s documentation warns
against this and suggests using <code>compojure/context</code> to scope usage of
<code>friend/wrap-authorize</code>. This doesn&rsquo;t solve the problem but it at
least narrows its scope. We can do better.</p>

<p>Compojure
<a href="https://github.com/weavejester/compojure/blob/master/HISTORY.md">1.2.0</a>
introduced the function <code>wrap-routes</code>. <code>wrap-routes</code> applies
middleware <em>after</em> a route is matched. By using this we can have all
of the benefits of using <code>friend/wrap-authorize</code> without breaking
returning 404 responses.</p>

<pre><code class="clojure">(ns example.server
  (:require [compojure.core :refer [GET defroutes] :as compojure]
            [compojure.route :as route]
            [cemerick.friend :as friend]))

(defroutes protected-routes
  (GET "/cars" _ (fetch-cars))
  (GET "/attributes" _ (fetch-attributes))
  (GET "/drivers" _ (fetch-drivers)))

(defroutes app
  (GET "/status" _ (status))
  (compojure/wrap-routes protected-routes
                         friend/wrap-authorize
                         #{::user})
  (route/not-found "NOT FOUND"))
</code></pre>

<p>There we have it. A solution without duplication that still responds
properly to requests for non-existent resources.
<code>compojure/wrap-routes</code> is a useful function to know about.</p>
]]></content>
  </entry>
  
</feed>
